buildscript {
  ext {
    grpcVersion = '1.14.0'
    protocVersion = '3.6.1'
  }
}

plugins {
  id 'idea'
  id 'java'
  id 'eclipse'
  id 'com.google.protobuf' version '0.8.6'
  id 'io.franzbecker.gradle-lombok' version '1.14'
  id 'org.springframework.boot' version '2.0.4.RELEASE'
  id 'io.spring.dependency-management' version '1.0.6.RELEASE'
}

version = '0.0.4'
group = 'daggerok'
sourceCompatibility = targetCompatibility = 1.8

lombok.version = '1.18.2'

repositories {
  mavenCentral()
}

dependencies {
  compile "io.grpc:grpc-netty:$grpcVersion"
  compile "io.grpc:grpc-protobuf:$grpcVersion"
  compile "io.grpc:grpc-stub:$grpcVersion"
  compile 'org.springframework.boot:spring-boot-starter-webflux'
  testCompile 'org.springframework.boot:spring-boot-starter-test'
  testCompile 'io.projectreactor:reactor-test'
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:$protocVersion"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

sourceSets {
  main {
    java {
      srcDirs "$buildDir/generated/source/proto/main/grpc"
      srcDirs "$buildDir/generated/source/proto/main/java"
    }
  }
}

wrapper {
  gradleVersion = '4.9'
}

defaultTasks 'clean', 'build'
